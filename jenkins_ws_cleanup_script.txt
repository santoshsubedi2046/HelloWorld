import hudson.FilePath;
import jenkins.model.*;

def deleted = []
def oneDayAgo = new Date() - 1
def jenkinsInstance = jenkins.model.Jenkins.instance

jenkinsInstance.nodes.each { hudson.model.Node node ->
  if (node.toComputer().isOnline()) {
    node.workspaceRoot.listDirectories().each { hudson.FilePath path ->
      def pathName = path.getRemote()
      if (path.name.startsWith(".")) {
        println "Skipping internal dir $node.displayName:$pathName"
      } else {
        def lastModified = new Date(path.lastModified())
        if (lastModified < oneDayAgo) {
          println "Deleting workspace at $node.displayName:$pathName (last modified $lastModified)"
          path.deleteRecursive()
          deleted << "$node.displayName:$pathName"
        } else {
          println "Skipping workspace at $node.displayName:$pathName (last modified $lastModified)"
        }
      }
    }
  } else {
    println "Skipping offline node: $node.displayName"
  }
}

"Deleted workspaces: \n\t" + deleted.sort().join("\n\t")
